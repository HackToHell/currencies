apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    namespace 'de.salomax.currencies'
    compileSdkVersion 33
    buildToolsVersion '33.0.1'

    defaultConfig {
        applicationId 'de.salomax.currencies'
        minSdkVersion 26
        targetSdkVersion 33
        // SemVer
        versionName = '1.17.7'
        versionCode = 11707
        archivesBaseName = "$applicationId-v$versionCode"
    }

    signingConfigs {
        release {
            if (getSecret('KEYSTORE_FILE') != null) {
                storeFile     = file(getSecret('KEYSTORE_FILE'))
                storePassword = getSecret('KEYSTORE_PASSWORD')
                keyAlias      = getSecret('KEYSTORE_KEY_ALIAS')
                keyPassword   = getSecret('KEYSTORE_KEY_PASSWORD')
            }
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            debuggable false
            jniDebuggable false
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"),
                    'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix ' [DEBUG]'
        }
    }

    flavorDimensions "version"
    productFlavors {
        play {
            dimension "version"
        }
        fdroid {
            dimension "version"
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    lint {
        disable 'MissingTranslation'
    }
}

dependencies {
    // kotlin
    implementation 'androidx.core:core-ktx:1.9.0'
    // support libs
    def appCompatVersion = '1.6.0-rc01'
    implementation "androidx.appcompat:appcompat:$appCompatVersion"
    implementation "androidx.appcompat:appcompat-resources:$appCompatVersion"
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.window:window:1.0.0'
    implementation 'com.google.android.material:material:1.7.0'
    // downloader
    def fuelVersion = '2.3.1'
    implementation "com.github.kittinunf.fuel:fuel:$fuelVersion"
    implementation "com.github.kittinunf.fuel:fuel-android:$fuelVersion"
    implementation "com.github.kittinunf.fuel:fuel-moshi:$fuelVersion"
    implementation "com.github.kittinunf.fuel:fuel-coroutines:$fuelVersion"
    def moshiVersion = '1.14.0'
    implementation "com.squareup.moshi:moshi-kotlin:$moshiVersion"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshiVersion"
    // math
    implementation 'org.mariuszgromada.math:MathParser.org-mXparser:5.0.7'
    // charts
    implementation 'com.robinhood.spark:spark:1.2.0'
    // test
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:4.8.1'
}

private String getSecret(String key) {
    File secretsFile = rootProject.file("secrets.properties")
    if (secretsFile.exists()) {
        Properties props = new Properties()
        props.load(new FileInputStream(secretsFile))
        return props.getProperty(key)
    } else {
        return null
    }
}

// versionCode <-> versionName /////////////////////////////////////////////////////////////////////

/**
 * Checks if versionCode and versionName match.
 * Needed because of F-Droid: both have to be hard-coded and can't be assigned dynamically.
 * So at least check during build for them to match.
 */
task checkVersion doLast {
    int versionCode = android.defaultConfig.versionCode
    int correctVersionCode = generateVersionCode(android.defaultConfig.versionName)
    if (versionCode != correctVersionCode)
        throw new GradleException(
                "versionCode and versionName don't match: " +
                        "versionCode should be $correctVersionCode. Is $versionCode."
        )
}
assemble.dependsOn checkVersion

/**
 * Checks if a fastlane changelog for the current version is present.
 */
task checkFastlaneChangelog doLast {
    int versionCode = android.defaultConfig.versionCode
    File changelogFile = file("$rootDir/fastlane/metadata/android/en-US/changelogs/${versionCode}.txt")
    if (!changelogFile.exists())
        throw new GradleException(
                "Fastlane changelog missing: expecting file '$changelogFile'"
        )
}
build.dependsOn checkFastlaneChangelog

/**
 * Generates a versionCode based on the given semVer String.
 * See e.g. https://proandroiddev.com/keep-salinity-with-your-versioncode-db2089b640b9
 *
 * @param semVer e.g. 1.3.1
 * @return e.g. 10301 (-> 1 03 01)
 */
private static int generateVersionCode(String semVer) {
    return semVer.split('\\.')
            .collect { Integer.parseInt(it) }
            .inject { sum, value -> sum * 100 + value }
}
